plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

repositories {
    mavenCentral()
    maven {
        url = uri("https://repo.codemc.io/repository/nms/")
    }

    maven {
        url = uri("https://hub.spigotmc.org/nexus/content/repositories/snapshots/")
    }

    maven {
        url = uri("https://repo.extendedclip.com/content/repositories/placeholderapi/")
    }

    maven {
        url = uri("https://jitpack.io")
    }

    maven {
        url = uri("https://repo.aikar.co/content/groups/aikar/")
    }

    maven {
        url = uri("https://repo.glaremasters.me/repository/public/")
    }
}

group = "com.nametagedit"
version = "4.5.23"
description = "NametagEdit"
def spigotVersion = "1.20.4-R0.1-SNAPSHOT"

java.sourceCompatibility = JavaVersion.VERSION_1_8
java.targetCompatibility = JavaVersion.VERSION_1_8
compileJava.options.compilerArgs << "-parameters"


configurations {
    shade.extendsFrom(implementation)
}

sourceSets.configureEach { ss ->
    ss.compileClasspath += project.configurations.shade
}

dependencies {
    shade "com.zaxxer:HikariCP:5.1.0"
    shade "co.aikar:acf-paper:0.5.1-SNAPSHOT"
    shade "org.bstats:bstats-bukkit:3.0.2"
    shade "org.slf4j:slf4j-jdk14:1.7.32"

    compileOnly "org.spigotmc:spigot-api:${spigotVersion}"
    compileOnly("org.spigotmc:spigot:${spigotVersion}") {
        exclude group: "com.mojang" // To allow building against Java 8
    }

    compileOnly "com.google.guava:guava:32.0.0-jre"
    compileOnly "commons-lang:commons-lang:2.6"
    compileOnly("com.googlecode.json-simple:json-simple:1.1.1")
    compileOnly "org.projectlombok:lombok:1.18.34"
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    compileOnly "net.luckperms:api:5.4"
    compileOnly "me.clip:placeholderapi:2.10.9"
    compileOnly "me.glaremasters:guilds:3.5.6.6-SNAPSHOT"
    compileOnly files("lib/PermissionsEx.jar")
    compileOnly files("lib/zPermissions.jar")
    compileOnly files("lib/EssentialsGroupManager.jar")
    compileOnly files("lib/LibsDisguises-10.0.25.jar")
}

tasks {
    jar {
        archiveFileName = "original-${project.description}.jar"
    }

    shadowJar {
        archiveFileName = "${project.description}.jar"
        configurations = [project.configurations.shade]
        minimize()
    }

    build {
        dependsOn(tasks.shadowJar)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
